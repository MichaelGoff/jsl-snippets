Names Default To Here( 1 );

// Open up Big Class, run all of the table scripts, then collect all the generated reports
dt = Open("$SAMPLE_DATA/Big Class.jmp");

script_names = dt << Get Table Script Names;
for(i = 1, i <= NItems(script_names), i++,
	dt << Run Script(script_names[i]);
);

jmp_reports = Find All( Reports );

// Pick a JMP Live connection
lc = New JMP Live( Connection( "livedemo" ) );

// Create a folder to contain all the Big Class Reports
// Opt to use it if it already exists
bigClassFolder = (lc << Create Folder(
	Parent Folder ("~"),
	Title("Big Class Reports"),
	If Exists("use"),
)) << As Scriptable;

// For every open report create new JMP Live Content and store it in a list
jmpLiveContentList = {};
for(i = 1, i <= N Items(jmp_reports), i++,
	Insert Into(
		jmpLiveContentList,
		New JMP Live Content(
			jmp_reports[i],
			Title("Big Class Report: " || Char(i)),
			Description("Inserted Big Class Report Description: " || Char(i)),
		)
	);
);

// Publish all created content to the big class folder
jmpliveresult = bigClassFolder << Publish( jmpLiveContentList );
postlist = jmpliveresult << As Scriptable();
// Log all of the created content
For( i = 1, i <= postlist << Get Number Of Items, i += 1,
	Write( "\!n\!nPost[", i, "]", "(ID): ", postlist[i] << Get ID );
	Write( "\!nPost[", i, "]", "(Type): ", postlist[i] << Get Type );
	Write( "\!nPost[", i, "]", "(Title): ", postlist[i] << Get Title );
);

// Clean up all opened reports
Close All(Reports);
Close(dt, No Save);